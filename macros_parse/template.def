fncode: parse_template($def, $template_file)
    $call check_path

    $global $template_idx=0
    $template_idx++
    my $name="_T$template_idx"

    my $cur_source=[]
    $(set:parent=$def)
    $call add_subcode, $cur_source, "template", $name
    $call read_source
    return $name

    #-------------------------------------- 
    subcode: read_source
        &call open_r, $template_file
            push @$cur_source, $_

    subcode: check_path
        my $template_dir
        $if $def->{macros}->{TemplateDir}
            $template_dir=$def->{macros}->{TemplateDir}
        $elif $MyDef::var->{TemplateDir}
            $template_dir=$MyDef::var->{TemplateDir}

        $if $template_dir
            $if $template_file!~/^\.*\//
                $template_file = $template_dir.'/'.$template_file
