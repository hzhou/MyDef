subcode:: import_file_init
    my $curindent=0

    my $codetype = "top"
    my $codeindent = 0
    my $codeitem = $def

macros:
    top_scope: $curindent == $codeindent and $codetype ne "macro"
    in_code: $codetype eq "code" and ($codeindent>0 or $in_default_page)
    in_macro: $codeindent>0 and $codetype eq "macro"
    in_page: $codetype eq "page" and (($in_default_page and $codeindent==0)  or (!$in_default_page and $codeindent==1))

subcode:: import_file_init
    my @indent_stack

subcode: start_code_indent(type, indent, item)
    # $call @debug
    $(if:type=page)
        $if !@indent_stack
            push @indent_stack, [$codetype, $codeindent, $codeitem]
    $(else)
        push @indent_stack, [$codetype, $codeindent, $codeitem]
    $codetype   = "$(type)"
    $codeindent = $(indent)
    $codeitem   = $(item)
    # prevent starting empty line triger dedent
    $curindent=$(indent)
    $lastindent = $curindent

    subcode: debug
        my $n = @indent_stack
        $print [$n]start_code_indent $(type) - $(indent) - $(item)

subcode: check_end_codeindent
    $while $curindent <$codeindent or ($in_default_page and $line=~/^END/ and $curindent==0 and @indent_stack)
        $if $codetype eq "code"
            $call @check_subcode_end
        my $t = pop @indent_stack
        ($codetype, $codeindent, $codeitem) = @$t
        $lastindent = $codeindent
        $if $codetype eq "code"
            $source = $codeitem->{source}
            my $src_location="SOURCE: $cur_file - $cur_line";
            push @$source, $src_location
    $if !@indent_stack
        # so we can differentiate the default main page code 
        #     from top subcode
        undef $page

#---------------------------------------- 
subcode:: import_file_init
    my $pages=$def->{pages}
    my $pagelist=$def->{pagelist}
    my $codes=$def->{codes}
    my $macros=$def->{macros}

#---------------------------------------- 
subcode:: import_file_init
    my $lastindent;

subcode: get_source_indent(curindent)
    $while $(curindent)>$lastindent
        $lastindent++;
        push @$source, "SOURCE_INDENT";

subcode: get_source_dedent(curindent)
    $while $(curindent)<$lastindent
        $lastindent--;
        push @$source, "SOURCE_DEDENT";

#---------------------------------------- 
subcode: get_parent(what)
    my $parent
    $if $curindent==0
        $parent = $def
    $elif !$in_default_page and $curindent==1 and $page
        $parent = $page
    $else
        $parent = $codeitem

    $if !$parent->{$(what)}
        $$(what) = {}
        $parent->{$(what)}=$$(what)
    $else
        $$(what) = $parent->{$(what)}

