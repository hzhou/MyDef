subcode: parsecode_case_support(if, elif, style)
    $global $case_if="$(if)", $case_elif="$(elif)"

    $global @case_stack
    $global $case_state
    $call debug_case_l

    $if $l=~/^\x24(if|elif|elsif|elseif|case)\s+(.*)$/
        my $cond=$2
        my $case=$case_if
        $call check_case_if
        $(if:parse_condition)
            $cond=parse_condition($cond)
        &call return_newblock, if
            $if $case eq $case_if
                $call if_$(style)
            $else
                $call elif_$(style)
            $call case_push, "if"
    $elif $l=~/^\$else/
        $if !$case_state and $l!~/NoWarn/i
            $call warn, Dangling \$else
        &call return_newblock, else
            $call else_$(style)
            $call case_push, undef
    $elif $l!~/^SUBBLOCK/
        # ***************
        undef $case_state
        $if $l eq "CASEPOP"
            $call case_pop
            return 0

    # -------------------------------- 
    subcode: check_case_if
        $if $1 eq "if"
        $elif $1 eq "case"
            $if !$case_state
                $case=$case_if
            $else
                $case=$case_elif
        $else
            $case=$case_elif

#---------------------------------------- 
subcode: case_push(state)
    push @src, "PARSE:CASEPOP"
    push @case_stack, {state=>$(state)}

    undef $case_state
    $call debug_case_push

subcode: case_pop
    $call debug_case_pop
    my $t_case=pop @case_stack
    $if $t_case
        $case_state=$t_case->{state}

#---- called from $list function ---
subcode: case_reset
    $global @case_stack
    $global $case_state
    @case_stack=()
    undef $case_state
    $call debug_case_reset

#----------------------------------------------- 
#----- c style ----------------------
#-- $call parsecode_case_support, if, else if, c_style
subcode:0 if_c_style
    $call push_single_block, "if ($cond) {", "}"

subcode:0 elif_c_style
    $call push_single_block, "else if ($cond) {", "}"

subcode:0 else_c_style
    $call push_single_block, "else {", "}"

#---- sh style ----------------
#-- $call parsecode_case_support, if, elif, sh_style
subcode:0 if_sh_style
    $call push_single_block, "if $cond; then", "fi"

subcode:0 elif_sh_style
    $call else_merge, fi
    $call push_single_block, "elif $cond; then", "fi"

subcode:0 else_sh_style
    $call else_merge, fi
    $call push_single_block, "else", "fi"

subcode:0 else_merge(fi)
    $if $out->[-1] ne "$(fi)"
        $call warn, "case: else missing $(fi) - [$out->[-1]]"
    pop @$out

# -------------------------------------------
subcode: debug_case_push
    $if $debug eq "case"
        my $level=@case_stack
        print "Entering case [$level]: $l\n"
subcode: debug_case_pop
    $if $debug eq "case"
        my $level=@case_stack
        print "    Exit case [$level]\n"
subcode: debug_case_l
    $if $debug eq "case"
        my $level=@case_stack
        print "        $level:[$case_state]$l\n"

subcode: debug_case_reset
    $if $debug eq "case"
        $print "    CASE RESET\n"
