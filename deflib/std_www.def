
## Frames ---------------------------------------------------------
htmlcode: basic_frame
    $call @process
    <!doctype html>
    $html
	$head
	    DUMP_STUB meta
	    $call @meta
	    $call @script
	$body
	    $call @content

htmlcode: js_frame
    $call @css_global
    <!doctype html>
    $html
	$head
	    DUMP_STUB meta
	    $call @meta
	    $script
		DUMP_STUB js_init
		$call @js_global
		&call setf, window.onload
		    $call @js_onload
	$body
	    $call @content

## HTML ------------------------------------------------------
subcode: meta_refresh(time, content)
    PRINT <meta http-equiv="refresh" content="$(time);url=$(content)">

subcode: js_src(url)
    <script type="text/javascript" src="$(url)"></script>

# form_input -- one row
htmlcode: form_input(prompt, varname)
    $tr
        $td label
            PRINT $(prompt)
        $td input
	    $(if:varname~pass)
		$call input_pass, $(varname)
	    $(else)
		$call input_text, $(varname)

htmlcode: input_hidden(name, value)
    <input type="hidden" name="$(name)" value="$(value)">

htmlcode: input_radio(name, value, label)
    <input type="radio" name="$(name)" value="$(value)"> $(label) 

htmlcode: input_checkbox(name, label)
    <input type="checkbox" name="$(name)"> $(label)

htmlcode: input_text(name)
    <input type="text" class="textinput" name="$(name)">

htmlcode: input_pass(name)
    <input type="password" class="textinput" name="$(name)">

htmlcode: input_submit(label)
    <input type="submit" value="$(label)">

## JavaScript -----------------------------------------------------
jscode: setf(target)
    $(allow_recurse:10)
    $(target) = function(){
	BLOCK
    };

jscode: setfa(target, @arg)
    $(allow_recurse:10)
    $(target) = function($(arg)){
	BLOCK
    };

jscode: listen(obj, event)
    $(obj).addEventListener("$(event)", function(event){
	BLOCK
    }, false);

jscode: setInterval(name, time)
    $global $(name)
    $(name)=window.setInterval(function(){
	BLOCK
    }, $(time));

jscode: auto
    (function() {
	BLOCK
    }());

jscode: load_element(name)
    $global $(name)
    $(name) = document.querySelector("#$(name)");

jscode: animate
    $global time_old=0
    $function animate_update(time)
	var dt=time-time_old
	$if dt>30
	    BLOCK
	    time_old=time
	window.requestAnimationFrame(animate_update)
    animate_update(0)

## PHP  -----------------------------------------------------

phpcode: echobig(string)
    $splitString = str_split($(string), 8192)
    $foreach $splitString as $chunk
	echo $chunk;

## ---------------------------------------------------------------------
phpcode: getparam(var)
    $if array_key_exists('$(var)', $_POST)
	$$(var)=$_POST['$(var)'];
    $elif array_key_exists('$(var)', $_GET)
	$$(var)=$_GET['$(var)'];

phpcode: loadget(var)
    $if array_key_exists('$(var)', $_GET)
	$$(var)=$_GET['$(var)'];

phpcode: loadpost(var)
    $if array_key_exists('$(var)', $_POST)
	$$(var)=$_POST['$(var)'];

phpcode: reqparam(var)
    $if array_key_exists('$(var)', $_POST)
	$$(var)=$_POST['$(var)'];
    $elif array_key_exists('$(var)', $_GET)
	$$(var)=$_GET['$(var)'];
    $else
	$errors[]="Missing $(var)"
	exit()
## ---------------------------------------------------------------------
phpcode: redirect(url)
    $if $siteroot
	header("Location: $siteroot/$(url)");
    $else
	header("Location: $(url)");
    exit;

phpcode: session_start
    session_start()
    $errors=array()
    $infos=array()

phpcode: setinfo
    $if !$_SESSION['infos']
	$_SESSION['infos']=array()
    $foreach  $infos as $i
	$_SESSION['infos'][]=$i;

phpcode: seterror
    $if !$_SESSION['errors']
	$_SESSION['errors']=array()
    $foreach  $errors as $i
	$_SESSION['errors'][]=$i;

phpcode: errorpage(errormsg)
    $h3 error
	PRINT $(errormsg)
    $if $_SESSION['infos']
	$foreach  $_SESSION['infos'] as $i
	    $infos[]=$i;
	unset($_SESSION['infos']);
    $if $_SESSION['errors']
	$foreach  $_SESSION['errors'] as $i
	    $errors[]=$i;
	unset($_SESSION['errors']);
    $call showerror
    exit();

phpcode: showinfo
    CSS: li.info {font-size: 12px; color: green; margin: 10px; }
    CSS: li.error {font-size: 12px; color: red; font-weight: 700; margin: 10px; }
    $if $infos
	$ul infobox
	    $foreach $infos as $i
		print "<li class=\"info\">$i</li>";

phpcode: showerror
    CSS: li.error {font-size: 12px; color: red; font-weight: 700; margin: 10px; }
    if($errors){
	$ul infobox
	    foreach ($errors as $i){
		print "<li class=\"error\">$i</li>";
	    }
    }

