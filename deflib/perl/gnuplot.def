include: ext.def

subcode: _autoload
    $global @title_list, @data_list
    $global @plot_settings, @plot_list, @splot_list

perlcode: plot_settings
    $call grab_codelist
    $if $codelist
        $foreach $t in @$codelist
            $if $t eq "NOOP"
                next
            $elif $t =~/^NEWLINE/
                push @$out, ""
                next
            MyDef::compileutil::expand_macro(\$t)
            push @$out, "push \@plot_settings, \"$t\";"

subcode: add_plot(@p)
    push @plot_list, "$(p)"

subcode: add_splot(@p)
    push @splot_list, "$(p)"

# ---- set macros ---------------------- 
subcode: set_title(@title)
    push @plot_settings, "set title '$(title)'"

subcode: set_xrange(x0, x1)
    push @plot_settings, "set xrange [$(x0):$(x1)]"

subcode: set_yrange(y0, y1)
    push @plot_settings, "set xrange [$(y0):$(y1)]"

subcode: set_xlabel(label)
    push @plot_settings, "set xlabel '$(label)'"

subcode: set_ylabel(label)
    push @plot_settings, "set ylabel '$(label)'"

#---- title_list, data_list  ----------------------------
subcode: batch_plot(@file)
    $(if:!file)
        $(set:file=t.pdf)
    &call get_plot, $(file)
        push @plot_settings, "set title '$title_list[0]'"
        $call set_x, $data_list[0]
        $for $i=1:@data_list
            $(set:D=$data_list[$i])
            $(set:T=$title_list[$i])
            $if ref($(D)) eq "ARRAY"
                $call add_y, $(D), $(T)
            $elif ref($(D)) eq "HASH"
                $if $(D)->{attr}
                    $(set:attr=$(D)->{attr})
                    $call add_hash_data
                $else
                    $call add_hash_data
            $elif $(D)=~/^x\s*=\s*(.*)/
                $call add_v_line, $1
            $elif $(D)=~/^set (.*)/
                push @plot_settings, $(D)
            $else
                $call add_function, $(D), $(T)

    subcode: add_hash_data
        $if $(D)->{r}
            $call add_xyr, $(D)->{x}, $(D)->{y}, $(D)->{r}, $(T)
        $else
            $call add_xy, $(D)->{x}, $(D)->{y}, $(T)

subcode: quick_plot(x, y, @datalist)
    &call get_plot, t.pdf
        $(if:x=-)
            $(if:y~@)
                my $n = $(y)
            $(else)
                my $n = @{$(y)}
            $call set_x_n, $n
        $(else)
            $call set_x, $(x)
        my $label='$(subst:y:[\$\@]:)'
        $call add_y, $(y), $label
        $(for:d in $(datalist))
            my $label='$(subst:d:[\$\@]:)'
            $call add_y, $(d), $label

#---- add_data ------------------------------------------- 
subcode: get_plot(file)
    my ($plot_x, $data_idx, $ls_idx)
    my $tempdata=tempname()
    BLOCK
    dump_plot("$(file)")
    $for $j=1:$data_idx+1
        unlink "$tempdata-$j.dat";

    # -------------------------------------- 
    subcode: set_x(x)
        $(if:x~@)
            $plot_x = \$(x)
        $(else)
            $plot_x = $(x)
    subcode: set_x_n(n)
        $plot_x=[]
        $for $i=0:$(n)
            push @$plot_x, $i
    subcode: set_x_range(n, x0, xn)
        $plot_x=[]
        my $dx = ($(xn)-$(x0))/$(n)
        $for $i=0:$(n)
            push @$plot_x, $(x0) + $i*$dx
    # ------------------
    subcode: append_attr(label, lw)
        $(if:linestyle)
            $ls_idx++
            $t.=" ls $ls_idx"

        $(if:attr)
            $t.=" $(attr)"
        $(elif:lw!=-)
            $t.=" lw $(lw)"

        $(if:label!=-)
            $(if:label~")
                $(set:label=$(label:strip))
            $t.=" title '$(label)'"

        push @plot_list, "    $t"

    # ----------------------------
    subcode: add_v_line(x)
        push @plot_settings, "set arrow from $(x),graph 0 to $(x),graph 1 nohead"

    subcode: add_function(f, label)
        my $t = "$(f) with lines"
        $call append_attr, $(label), 1

    subcode: add_y(y, label)
        $data_idx++ 
        my $name = "$tempdata-$data_idx.dat"
        $if !$plot_x
            $(if:y~@)
                my $n = $(y)
            $(else)
                my $n = @{$(y)}
            $call set_x_n, $n
        $(if:y~@)
            add_xy_data($name, $plot_x, \$(y))
        $(else)
            add_xy_data($name, $plot_x, $(y))
        my $t = "'$name' u 1:2 with lines"
        $call append_attr, $(label), 2

    subcode: add_xy(x, y, label)
        $data_idx++ 
        my $name = "$tempdata-$data_idx.dat"
        add_xy_data($name, $(x), $(y))
        my $t = "'$name' u 1:2 with lines"
        $call append_attr, $(label), 2
        
    subcode: add_points(x, y, label)
        $data_idx++ 
        my $name = "$tempdata-$data_idx.dat"
        add_xy_data($name, $(x), $(y))
        my $t = "'$name' u 1:2 with points"
        $call append_attr, $(label), -
        
    subcode: add_xyr(x,y,r, label)
        $data_idx++ 
        my $name = "$tempdata-$data_idx.dat"
        add_xy_data($name, $(x), $(y), $(r))
        my $t = "'$name' u 1:2:3 with errorbars"
        $call append_attr, $(label), 1

#----------------------------------------------- 
fncode: tempname
    $global $temp_seq=0
    $temp_seq++
    my $timestamp=time()
    $: my $t = "$timestamp.$$.$temp_seq";
    $if -d "/dev/shm"
        return "/dev/shm/$t"
    $elif -d "/tmp"
        return "/tmp/$t"
    $else
        return $t

fncode: add_xy_data($name, $x, $y, $r)
    my $n = @$x
    &call open_w, $name
        $if !$r
            $for $i=0:$n
                print Out "$x->[$i], $y->[$i]\n"
        $else
            $for $i=0:$n
                print Out "$x->[$i], $y->[$i], $r->[$i]\n"

fncode: dump_plot($file)
    $print "    --> [$file]"
    open Out, "|gnuplot" or die "Can't execute gnuplot\n";;
    $(set:print_to=Out)

    $global $plot_terminal
    $if $plot_terminal
        $print "set terminal $plot_terminal"
    $elif $file=~/\.pdf$/
        $(set:size=size 4,3)
        $(for:l,r,b,t and 0.1, 0.9, 0.15, 0.85)
            push @plot_settings, "set $1margin at screen $2;"
        $print "set terminal pdfcairo $(size) enhanced color"
    $elif $file=~/\.eps$/
        $print "set terminal postscript enhanced color"
    $elif $file=~/\.png$/
        $print "set terminal pngcairo"
    $else
        $print "set terminal pdfcairo"
    $print "set output '$file'"

    $foreach $l in @plot_settings
        $print $l
    $if @plot_list
        $print "plot -"
        print Out join(",\\\n    ", @plot_list), "\n"
    $if @splot_list
        $print "splot -"
        print Out join(",\\\n    ", @splot_list), "\n"
    close Out

#----------------------------------------------- 
macros:
    COLOR_line: 0072bd,d95319,edb120,7e2f8e,77ac30,4dbeee,a2142f
    #           blue orange yellow purple green light-blue red
    COLOR_jet:  000090,000fff,0090ff,0fffee,90ff70,ffee00,ff7000,ee0000,7f0000
    #           blue ... red
    COLOR_dark2: 1B9E77,D95F02,7570B3,E7298A,66A61E,E6AB02,A6761D,666666

