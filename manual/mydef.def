# A manual in a style of https://www.gnu.org/software/m4/manual/m4.html
include: html/code.def

include: intro.def
include: install.def
include: syntax.def
include: output_general.def
include: output_perl.def

page: mydef, basic_frame
    module: www
    title: MyDef Manual

    $call css_manual
    $call css_code
    $h1
        $(title)
    $call TOC
    $call @sections
    $call section, c, $(code:output_c)
    $p
        [To be continued.]
    $call section, www, $(code:output_www)
    $p
        [To be continued.]
    $call section, python, $(code:output_python)
    $p
        [To be continued.]
    $call section, java, $(code:output_java)
    $p
        [To be continued.]

    $call end_section

#---------------------------------------- 
subcode: TOC
    $(anchor:TOC)
    $h2 contents-heading
        Table of Contents
    $div contents
        $ul no-bullet id1 
            DUMP_STUB _toc

subcode: section(name, @title)
    $call end_section
    $(setmacro:id1+=1)
    $(block:_toc)
        <li><a href="#$(name)">$(id1) $(title)</a>
    $(setmacro:id2=0)
    # --------------------
    $(anchor:$(name))
    $h2 chapter
        $(id1) $(title)

subcode: subsection(name, @title)
    $(block:_toc)
        $(if:id2=0)
            <ul class="no-bullet id2">
        $(setmacro:id2+=1)
        <li><a href="#$(name)">$(id1).$(id2) $(title)</a></li>
    # --------------------
    $(anchor:$(name))
    $h3 section
        $(id1).$(id2) $(title)

subcode: end_section
    $(block:_toc)
        $(if:id1>0)
            $(if:id2>0)
                </ul>
            </li>

subcode: css_manual
    CSS: body {padding: 50px}
    CSS: ul.no-bullet {list-style: none; padding: 0}
    CSS: ul.id1 {font-weight:bold}
    CSS: ul.id2 {font-weight:normal}
    CSS: li {margin: 0.5em 1em; line-height:1.3em;}
    CSS: a[href] {color: #005090}
    CSS: a {text-decoration: none; outline-style:none;}
    CSS: pre {border-radius: 0.3em; background-color: #f2efe4}

subcode: css_code
    CSS: pre {margin: 4px 10px; padding-left: 20px;}
    CSS: pre strong {color: #444; font-weight:700}
    CSS: .mydef-comment {color: #888; font-style: italic} # gray
    CSS: .mydef-label  {color: #22f;} # blue
    CSS: .mydef-label2 {color: #228;} # blueish
    CSS: .mydef-keyword {color: #494; font-weight: 700} # green
    CSS: .mydef-preproc {color: #844;} # reddish
    CSS: .mydef-include {color: #444; text-decoration: underline;}
    CSS: .mydef-quote {color: #a2a;}
    CSS: .mydef-macro {color: #474;}
    CSS: .mydef-special {color: #888; font-weight: 700}

subcode:: other_code_filters
    $case $type eq "sh"
        $call sh_filter
