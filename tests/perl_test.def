page: test
    type: pl
    subcode: main
        $call test_read_file
        $call test_sub
        $call test_for
        $call test_case, 1
        $call test_case, 2

    subcode: test_read_file
        $(set:f=../perl_test.def)
        print "\nTest \&call open_r $(f)...\n"
        &call read_sub
            print "    subcode: $1\n"

        subcode: read_sub
            &call open_r, $(f)
                $if /^\s*subcode:\s+(\w+)/
                    BLOCK

    subcode: test_sub
        print "\nTest \$sub and \$global ...\n"
        test_global("Hui")
        test_global("Xiaoyi")
        test_global("Harry")

        $sub test_global($name)
            $global $index
            $index++
            print "    $index: $name\n"

    subcode: test_for
        print "\nTest \$for ...\n"
        print "  \$for \$i=0:10 (with , )\n    "
        $for $i=0:10
            print $i
            $if $i<10-1
                print ", "

        print "\n  \$for \$i=10:0\n    "
        $for $i=10:0
            print $i, " "
        print "\n"

# ------------------------------------
subcode: test_case(option)
    # DEBUG case
    print "\nTest \$case option $(option)...\n"
    my $t=4
    print " * \$case -> if/elsif: 4 < 5\n"
    $case $t<5
        print "$t < 5\n"
    $case $t<10
        print "5 <= $t < 10\n"

    $(if:option=2)
        print " \$case restart before \$call othercase ...\n"
    $call othercase

    $case $t<1000
        print "100 <= $t < 1000\n"
    $else
        print "1000 <= $t\n"

subcode: othercase
    $case $t<100
        $if $t<5
            print "$t < 5\n"
        $case $t<10
            print "5 <= $t < 10\n"
        $else
            print "$t >= 10\n"

